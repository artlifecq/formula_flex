<?xml version="1.0" encoding="utf-8"?>
<!---
*
* 特效文件列表项
* @author L.L.M.Sunny
* 创建时间：2015-7-17 上午10:30:12
*
-->
<s:NavigatorContent xmlns:fx="http://ns.adobe.com/mxml/2009"
					xmlns:s="library://ns.adobe.com/flex/spark"
					xmlns:mx="library://ns.adobe.com/flex/mx"
					width="100%"
					height="100%"
					label="未命名"
					elementAdd="navigatorcontent1_elementAddHandler(event)">
	<fx:Script>
		<![CDATA[
			import com.editor.cfg.ConfigData;
			import com.editor.cfg.EffectResConfig;
			import com.editor.data.ConfigDesc;
			import com.editor.data.InputType;
			import com.editor.data.InternalTabelName;
			import com.editor.data.TabelData;
			import com.editor.events.ConfigListEvent;
			import com.editor.events.EffectFileListEvent;
			import com.editor.manager.DataStructuresManager;

			import mx.collections.ArrayCollection;

			import spark.events.ElementExistenceEvent;

			import org.client.mainCore.manager.EventManager;
			private var _effectItems : ArrayCollection;

			protected function data_effect_selectionChangeHandler(event : MouseEvent) : void
			{
				var tabel : TabelData = DataStructuresManager.getInstance().getTabel(InternalTabelName.EffectResConfigName);
				var configDesc : ConfigDesc = tabel ? tabel.getConfigDesc() : null;
				if (!configDesc)
					return;
				var effectCfg : EffectResConfig = data_effect.selectedItem as EffectResConfig;
				if (!effectCfg)
					return;
				EventManager.dispatchEvent(ConfigListEvent.CONFIG_SELECTED, effectCfg);
				
				RoleActionView.open();
				if (RoleActionView.window)
				{
					RoleActionView.window.updateAvatarData();
				}
			}

			public function destroy() : void
			{
				_effectItems = null;
			}

			public function setEffectItems(items : ArrayCollection) : void
			{
				_effectItems = items;
				if (data_effect)
				{
					data_effect.dataProvider = _effectItems;
					var tabel : TabelData = DataStructuresManager.getInstance().getTabel(InternalTabelName.EffectResConfigName);
					var configDesc : ConfigDesc = tabel ? tabel.getConfigDesc() : null;
					if (!configDesc)
						return;
					data_effect.selectedItem = configDesc.selectedData;
					var index : int = _effectItems.getItemIndex(configDesc.selectedData);
					if (index > -1)
						callLater(data_effect.scrollToIndex, [index]);
				}
			}

			public function getEffectItems() : ArrayCollection
			{
				return _effectItems;
			}

			protected function data_effect_doubleClickHandler(event : MouseEvent) : void
			{
				var effectObj : EffectResConfig = data_effect.selectedItem as EffectResConfig;
				if (!effectObj)
					return;
				var window : ConfigEditView = ConfigEditView.open("特效资源设置", effectObj);
				var inputTypes : Dictionary = new Dictionary();
				inputTypes["res"] = new InputType("string", false);
				window.setData(effectObj, onPullData, onEditViewDragDropCallback, inputTypes, null, onOkCallback);
			}

			private function onEditViewDragDropCallback(dataField : String, data : ConfigData, dropData : Object) : void
			{
				if (data is EffectResConfig)
				{
				}
			}

			protected function navigatorcontent1_elementAddHandler(event : ElementExistenceEvent) : void
			{
				var tabel : TabelData = DataStructuresManager.getInstance().getTabel(InternalTabelName.EffectResConfigName);
				var configDesc : ConfigDesc = tabel ? tabel.getConfigDesc() : null;
				if (!configDesc)
					return;
				data_effect.selectedItem = configDesc.selectedData;
				var index : int = _effectItems.getItemIndex(configDesc.selectedData);
				if (index > -1)
					callLater(data_effect.scrollToIndex, [index]);
			}

			private function onPullData(dataField : String, data : ConfigData) : void
			{
			}

			private function onOkCallback(data : ConfigData) : void
			{
				EventManager.dispatchEvent(EffectFileListEvent.UPDATE_EFFECT_FILE_LIST);
			}

			[Bindable]
			/** 换装数据 **/
			public function get effectItems() : ArrayCollection
			{
				return _effectItems;
			}

			/**
			 * @private
			 */
			public function set effectItems(value : ArrayCollection) : void
			{
				_effectItems = value;
			}
		]]>
	</fx:Script>
	<s:Group left="0"
			 right="0"
			 top="0"
			 bottom="0"
			 width="100%"
			 height="100%">
		<mx:DataGrid id="data_effect"
					 x="0"
					 y="0"
					 width="100%"
					 height="100%"
					 dataProvider="{effectItems}"
					 click="data_effect_selectionChangeHandler(event)"
					 editable="false"
					 dragEnabled="true"
					 doubleClickEnabled="true"
					 doubleClick="data_effect_doubleClickHandler(event)">
			<mx:columns>
				<mx:DataGridColumn headerText="名称"
								   dataField="name"
								   width="100"/>
				<mx:DataGridColumn headerText="资源"
								   dataField="mRes"/>
			</mx:columns>
		</mx:DataGrid>
	</s:Group>
</s:NavigatorContent>
