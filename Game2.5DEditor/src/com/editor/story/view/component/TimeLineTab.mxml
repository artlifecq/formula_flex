<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 width="100" height="100" y="37" creationComplete="group1_creationCompleteHandler(event)">
	<fx:Script>
		<![CDATA[
			import mx.core.UIComponent;
			import mx.events.FlexEvent;
			
			import org.client.mainCore.ds.HashMap;
			[Bindable]
			private var _mask:UIComponent;
			private var _tabMap:HashMap = new HashMap();
			
			/**
			 * 创建标签
			 * @param tabID	标签id
			 * @param txt	文本内容
			 * @author 杨剑明	  2015-11-24
			 */
			public function createTab(tabID:int, txt:String, tips:String):void
			{
				var tab:TimeLineTabComponent = _tabMap.getValue(tabID);
				if(tab)
				{
					if(tips != null)
					{
						tab.toolTip = tips;
					}
					return;
				}
				tab = new TimeLineTabComponent();
				tab.toolTip = tips;
				tab.createTab(tabID,txt);
				tab_group.addElement(tab);
				_tabMap.add(tabID,tab);
			}
			
			/**
			 * 移除一个tab
			 * @param tabID	标签id
			 * @author 杨剑明	  2015-11-24
			 */
			public function removeTab(tabID:int):void
			{
				var tab:TimeLineTabComponent = _tabMap.getValue(tabID);
				if(tab_group.contains(tab))
				{
					tab_group.removeElement(tab);
					_tabMap.remove(tabID);
				}
			}
			
			/**
			 * 移除所有tab
			 * @author 杨剑明	  2015-11-24
			 */
			public function removeAllTab(needClear:Boolean = true):void
			{
				if(_tabMap.length == 0)
				{
					return;
				}
				
				tab_group.removeAllElements();
				tab_group.y = 42;
				if(needClear)
				{
					_tabMap.clear();
				}
			}
			
			protected function group1_creationCompleteHandler(event:FlexEvent):void
			{
				_mask = new UIComponent();
				_mask.graphics.beginFill(0xFF0000);
				_mask.graphics.drawRect(0,42,100,175);
				_mask.graphics.endFill();
				addElement(_mask);
			}
			
			/**
			 * 总时间
			 * @author 杨剑明	  2015-11-26
			 */
			public function setMaxTime(time:int):void
			{
				total_time.text = "总时间：" + time;
			}
			
			/**
			 * 当前时间
			 * @author 杨剑明	  2015-11-26
			 */
			public function setCurTime(time:int):void
			{
				cur_time.text = "当前时间：" + time;
			}
			
		]]>
	</fx:Script>
	<s:BorderContainer id="container" width="100" height="42" y="0" backgroundColor="0x5f5f5f">
		<s:Label id="total_time" text="总时间：20000" textAlign="center" width="100%" height="22" backgroundColor="0x262626" verticalAlign="middle">
		</s:Label>
		<s:Label id="cur_time" text="当前时间：0" textAlign="center" width="100%" height="20" y="22" backgroundColor="0x5f5f5f" verticalAlign="middle">
		</s:Label>
	</s:BorderContainer>
	<s:VGroup id="tab_group" y="42" gap="0" mask="{_mask}">
		
	</s:VGroup>
</s:Group>
